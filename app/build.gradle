apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.phlox.tvwebbrowser"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 41
        versionName "1.5.7"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental":"true"]
            }
        }
    }
    signingConfigs {
        release {
            storeFile rootProject.file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "crashlytics", "appstore"
    productFlavors {
        crashlyticsfree {
            dimension "crashlytics"
        }
        crashlytics {
            dimension "crashlytics"
            //versionNameSuffix "-crashlytics"
        }

        google {
            dimension "appstore"
        }
        amazon {
            dimension "appstore"
        }
    }

    lintOptions {
        abortOnError false
    }

    kapt {
        arguments {
            //used when AppDatabase @Database annotation exportSchema = true. Useful for migrations
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-alpha07'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-alpha07'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha07'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0'

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    //implementation 'androidx.webkit:webkit:1.3.0'

    implementation 'com.github.truefedex:segmented-button:v1.0.0'
    implementation 'de.halfbit:pinned-section-listview:1.0.0'

    crashlyticsImplementation 'com.google.firebase:firebase-core:17.5.0'
    crashlyticsImplementation 'com.google.firebase:firebase-crashlytics-ktx:17.2.1'
}
