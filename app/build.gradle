apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.phlox.tvwebbrowser"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 25
        versionName "1.3.0"
    }
    signingConfigs {
        release {
            storeFile rootProject.file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "crashlytics"
    productFlavors {
        crashlyticsfree {
            dimension "crashlytics"
        }
        crashlytics {
            dimension "crashlytics"
            versionNameSuffix "-crashlytics"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def supportLibVersion = '28.0.0'
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    implementation 'com.github.truefedex:ASQL:v0.1.2'
    implementation 'de.halfbit:pinned-section-listview:1.0.0'

    crashlyticsImplementation 'com.google.firebase:firebase-core:16.0.8'
    crashlyticsImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
}
